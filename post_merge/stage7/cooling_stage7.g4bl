physics QGSP_BERT_EMX 

param -unset first=1
param -unset last=10

param ncells=55
param pi=3.1415926
param coilname1=coil1
param coilname2=coil2
param coilname3=coil3
param cell_length=700
param solpos1=0.10958719445122693
param coil_length1=88.90183840755043
param coil_radius1=50
param coil_width1=70
param current1=183.33333516781713
param solpos2=0.11010969571205828
param coil_length2=84.25976618166285
param coil_radius2=130
param coil_width2=80
param current2=153.85660217573397
param solpos3=0.28660611883239806
param coil_length3=213.2975698925071
param coil_radius3=220
param coil_width3=100
param current3=210.2947901424895
param angle=1.5
param bendpos=0.1
param dipole=0.175
param dipole_length=100
param pipe_radius=38
param pipe_diameter=2*$pipe_radius
param fringe_length=4*$pipe_radius
param rf_length=95
param rf_fre=0.704
param rf_grad=28.5 #29.462472080577413
param rf_ph=18.379 #17.75860166426884
param wedge_window_length=0.07
param rf_window_length=0.02
param wallthick=$rf_window_length
param half_wedge_length=100
param pipe_length=($cell_length-4*($rf_length+2*$wallthick))/2
param wedge_apex=130
param x_last=-1.98
param y_last=-2.38
param x_off=-1.5572606236555506
param y_off=-2.394758818128987
param wedge_x_off=-3 #-1.75
param ref_momentum=203

param pres=35

material H2 Z=1 A=1.01 density=0.000090*$pres pressure=$pres
param cavity_gas=Vacuum
#material cavity_gas H,0.99 O,0.01 density=0.0035 pressure=$pres
#param worldMaterial=H2

genericbend solbend fieldWidth=1000 fieldHeight=1000 fieldLength=$dipole_length \
               ironWidth=-1 ironHeight=-1 ironLength=-1 \
               openAperture=1 \
               By=$dipole kill=1 fringe=0 ironColor=1,1,1


fieldexpr fringe_left length=$fringe_length width=2*$pipe_radius height=2*$pipe_radius By=$dipole*((1+exp(5*-z/$pipe_diameter)*cos(5*y/$pipe_diameter))/(1+2*exp(5*-z/$pipe_diameter)*cos(5*y/$pipe_diameter)+exp(2*5*-z/$pipe_diameter))) Bz=-$dipole*exp(5*-z/$pipe_diameter)*sin(5*y/$pipe_diameter)/(1+2*exp(5*-z/$pipe_diameter)*cos(5*y/$pipe_diameter)+exp(2*5*-z/$pipe_diameter)) nX=2 nY=100 nZ=150 
fieldexpr fringe_right length=$fringe_length width=2*$pipe_radius height=2*$pipe_radius By=$dipole*((1+exp(5*z/$pipe_diameter)*cos(5*y/$pipe_diameter))/(1+2*exp(5*z/$pipe_diameter)*cos(5*y/$pipe_diameter)+exp(2*5*z/$pipe_diameter))) Bz=-$dipole*exp(5*z/$pipe_diameter)*sin(5*y/$pipe_diameter)/(1+2*exp(5*z/$pipe_diameter)*cos(5*y/$pipe_diameter)+exp(2*5*z/$pipe_diameter)) nX=2 nY=100 nZ=150 

coil $coilname1 innerRadius=$coil_radius1 outerRadius=$coil_radius1+$coil_width1 length=$coil_length1 material=Cu maxZ=$cell_length maxR=$coil_radius1+4*$coil_width1 #tolerance=0.02
solenoid sol1 coilName=$coilname1 current=$current1 kill=1 
solenoid sol2 coilName=$coilname1 current=-$current1 kill=1 

coil $coilname2 innerRadius=$coil_radius2 outerRadius=$coil_radius2+$coil_width2 length=$coil_length2 material=Cu maxZ=$cell_length maxR=($coil_radius2+4*$coil_width2) #tolerance=0.02
solenoid sol3 coilName=$coilname2 current=$current2 kill=1 
solenoid sol4 coilName=$coilname2 current=-$current2 kill=1 

coil $coilname3 innerRadius=$coil_radius3 outerRadius=$coil_radius3+$coil_width3 length=$coil_length3 material=Cu maxZ=$cell_length maxR=$coil_radius3+4*$coil_width3 #tolerance=0.01
solenoid sol5 coilName=$coilname3 current=$current3 kill=1 
solenoid sol6 coilName=$coilname3 current=-$current3 kill=1 

tubs pipe innerRadius=$pipe_radius-0.5 outerRadius=$pipe_radius-0.5+1 length=$pipe_length kill=1

pillbox cavity innerLength=$rf_length irisRadius=$pipe_radius-0.5 frequency=$rf_fre maxGradient=$rf_grad phaseAcc=$rf_ph winMat=Be cavityMaterial=$cavity_gas win1OuterRadius=10 win1Thick=$rf_window_length win2Thick=$rf_window_length wallThick=$wallthick collarThick=0 kill=1

box wedge_window height=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) length=$wedge_window_length width=2*$half_wedge_length/2/sin($wedge_apex/2*$pi/180) material=Be
trap all_wedge  height=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) upperWidth=0.001 lowerWidth=2*$half_wedge_length length=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) material=LH2


do i -2 $ncells
place sol1 z=$i*$cell_length+$solpos1*$cell_length
place sol3 z=$i*$cell_length+$solpos2*$cell_length 
place sol5 z=$i*$cell_length+$solpos3*$cell_length 
place sol6 z=$i*$cell_length+$cell_length-$solpos3*$cell_length 
place sol4 z=$i*$cell_length+$cell_length-$solpos2*$cell_length 
place sol2 z=$i*$cell_length+$cell_length-$solpos1*$cell_length
enddo

do i -2 $ncells
place solbend z=$i*$cell_length+$bendpos*$cell_length rename=solbend_a_$i
place solbend z=$i*$cell_length+$cell_length-$bendpos*$cell_length rename=solbend_b_$i
enddo

do i -2 $ncells
place fringe_left z=$i*$cell_length+$bendpos*$cell_length-$dipole_length/2-$fringe_length/2
place fringe_right z=$i*$cell_length+$bendpos*$cell_length+$dipole_length/2+$fringe_length/2+0.01
place fringe_left z=$i*$cell_length+$cell_length-$bendpos*$cell_length-$dipole_length/2-$fringe_length/2
place fringe_right z=$i*$cell_length+$cell_length-$bendpos*$cell_length+$dipole_length/2+$fringe_length/2+0.01
enddo

do i 0 $ncells
place pipe z=$i*$cell_length+$pipe_length/2
place pipe z=$i*$cell_length+$cell_length-$pipe_length/2
enddo


do i 0 $ncells
place cavity z=$i*$cell_length+$cell_length/2-($rf_length+2*$wallthick+$wallthick+$rf_length/2) rename=rf_b_$i
place cavity z=$i*$cell_length+$cell_length/2-($wallthick+$rf_length/2) rename=rf_c_$i
place cavity z=$i*$cell_length+$cell_length/2+($wallthick+$rf_length/2) rename=rf_d_$i
place cavity z=$i*$cell_length+$cell_length/2+($rf_length+2*$wallthick+$wallthick+$rf_length/2) rename=rf_e_$i
enddo

do i 0 $ncells
place all_wedge z=$i*$cell_length  rotation=X90,Y90 x=$x_off+$wedge_x_off rename=wedge_$i
enddo

do i 0 $ncells
place wedge_window z=$i*$cell_length-$half_wedge_length/2-$wedge_window_length/(cos($wedge_apex/2*$pi/180)) x=$x_off+$wedge_x_off rotation=Y-$wedge_apex/2 color=1,0,1
place wedge_window z=$i*$cell_length+$half_wedge_length/2+$wedge_window_length/(cos($wedge_apex/2*$pi/180)) x=$x_off+$wedge_x_off rotation=Y$wedge_apex/2 color=1,0,1
enddo


reference particle=mu+ referenceMomentum=$ref_momentum beamT=1315.56 beamX=$x_off beamY=$y_off noEfield=1 noBfield=1 noEloss=1
beam ascii file=beam_stage7.beam firstEvent=$first lastEvent=$last beamX=$x_off-$x_last beamY=$y_off-$y_last

trackcuts killSecondaries=1
#zntuple z=$cell_length filename=final
trace nTrace=1

#printfield type=grid file=field_cell.dat field=Bx,By,Bz X0=-180 Y0=-180 Z0=0 nX=61 dX=6 nY=61 dY=6 nZ=201 dZ=$cell_length/200
zntuple output zloop=0:$ncells*$cell_length:$cell_length format=for009 require=PDGid==-13 file=particles_info.txt referenceParticle=1