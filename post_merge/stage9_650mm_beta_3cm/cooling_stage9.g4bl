physics QGSP_BERT_EMX 

param -unset first=1
param -unset last=10

param ncells=70
param pi=3.1415926
param coilname1=coil1
param coilname2=coil2
param cell_length=650
param solpos1=0.09135133345069503
param coil_length1=86.83690788284794
param coil_radius1=30
param coil_width1=65
param current1=317.27020283715444
param solpos2=0.29978779301962455
param coil_length2=128.86514068355493
param coil_radius2=190
param coil_width2=180
param current2=228.656879196339
param angle=1.5
param bendpos=0.1
param dipole=0.17
param dipole_length=100
param pipe_radius=26
param pipe_diameter=2*$pipe_radius
param fringe_length=4*$pipe_radius
param rf_length=95
param rf_fre=0.704
param rf_grad=18.319525861568465
param rf_ph=18.67545408046572
param wedge_window_length=0.01
param rf_window_length=0.01
param wallthick=$rf_window_length
param half_wedge_length=75
param pipe_length=($cell_length-4*($rf_length+2*$wallthick))/2
param wedge_apex=140
param x_last=-0.89
param y_last=-1.72
param x_off=-0.9554588557845609
param y_off=-1.7775909449105503
param wedge_x_off=-3.25
param ref_momentum=199

param pres=35

material H2 Z=1 A=1.01 density=0.000090*$pres pressure=$pres
param cavity_gas=Vacuum
#material cavity_gas H,0.99 O,0.01 density=0.0035 pressure=$pres
#param worldMaterial=H2

genericbend solbend fieldWidth=1000 fieldHeight=1000 fieldLength=$dipole_length \
               ironWidth=-1 ironHeight=-1 ironLength=-1 \
               openAperture=1 \
               By=$dipole kill=1 fringe=0 ironColor=1,1,1


fieldexpr fringe_left length=$fringe_length width=2*$pipe_radius height=2*$pipe_radius By=$dipole*((1+exp(3.5*-z/$pipe_diameter)*cos(3.5*y/$pipe_diameter))/(1+2*exp(3.5*-z/$pipe_diameter)*cos(3.5*y/$pipe_diameter)+exp(2*3.5*-z/$pipe_diameter))) Bz=-$dipole*exp(3.5*-z/$pipe_diameter)*sin(3.5*y/$pipe_diameter)/(1+2*exp(3.5*-z/$pipe_diameter)*cos(3.5*y/$pipe_diameter)+exp(2*3.5*-z/$pipe_diameter)) nX=2 nY=100 nZ=150 
fieldexpr fringe_right length=$fringe_length width=2*$pipe_radius height=2*$pipe_radius By=$dipole*((1+exp(3.5*z/$pipe_diameter)*cos(3.5*y/$pipe_diameter))/(1+2*exp(3.5*z/$pipe_diameter)*cos(3.5*y/$pipe_diameter)+exp(2*3.5*z/$pipe_diameter))) Bz=-$dipole*exp(3.5*z/$pipe_diameter)*sin(3.5*y/$pipe_diameter)/(1+2*exp(3.5*z/$pipe_diameter)*cos(3.5*y/$pipe_diameter)+exp(2*3.5*z/$pipe_diameter)) nX=2 nY=100 nZ=150 

coil $coilname1 innerRadius=$coil_radius1 outerRadius=$coil_radius1+$coil_width1 length=$coil_length1 material=Cu maxZ=$cell_length maxR=$coil_radius1+4*$coil_width1 #tolerance=0.02
solenoid sol1 coilName=$coilname1 current=$current1 kill=1 
solenoid sol2 coilName=$coilname1 current=-$current1 kill=1 

coil $coilname2 innerRadius=$coil_radius2 outerRadius=$coil_radius2+$coil_width2 length=$coil_length2 material=Cu maxZ=$cell_length maxR=($coil_radius2+4*$coil_width2) #tolerance=0.02
solenoid sol3 coilName=$coilname2 current=$current2 kill=1 
solenoid sol4 coilName=$coilname2 current=-$current2 kill=1 

tubs pipe innerRadius=$pipe_radius-3.5 outerRadius=$pipe_radius-3.5+1 length=$pipe_length kill=1

pillbox cavity innerLength=$rf_length irisRadius=$pipe_radius-3.5 frequency=$rf_fre maxGradient=$rf_grad phaseAcc=$rf_ph winMat=Be cavityMaterial=$cavity_gas win1OuterRadius=10 win1Thick=$rf_window_length win2Thick=$rf_window_length wallThick=$wallthick collarThick=0 kill=1

box wedge_window height=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) length=$wedge_window_length width=2*$half_wedge_length/2/sin($wedge_apex/2*$pi/180) material=Be
trap all_wedge  height=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) upperWidth=0.001 lowerWidth=2*$half_wedge_length length=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) material=LH2


do i -2 $ncells
place sol1 z=$i*$cell_length+$solpos1*$cell_length rename=sol_a_$i
place sol3 z=$i*$cell_length+$solpos2*$cell_length rename=sol_b_$i
place sol4 z=$i*$cell_length+$cell_length-$solpos2*$cell_length rename=sol_c_$i
place sol2 z=$i*$cell_length+$cell_length-$solpos1*$cell_length rename=sol_d_$i
enddo

do i -2 $ncells
place solbend z=$i*$cell_length+$bendpos*$cell_length rename=solbend_a_$i
place solbend z=$i*$cell_length+$cell_length-$bendpos*$cell_length rename=solbend_b_$i
enddo

do i -2 $ncells
place fringe_left z=$i*$cell_length+$bendpos*$cell_length-$dipole_length/2-$fringe_length/2
place fringe_right z=$i*$cell_length+$bendpos*$cell_length+$dipole_length/2+$fringe_length/2+0.01
place fringe_left z=$i*$cell_length+$cell_length-$bendpos*$cell_length-$dipole_length/2-$fringe_length/2
place fringe_right z=$i*$cell_length+$cell_length-$bendpos*$cell_length+$dipole_length/2+$fringe_length/2+0.01
enddo

do i 0 $ncells
place pipe z=$i*$cell_length+$pipe_length/2
place pipe z=$i*$cell_length+$cell_length-$pipe_length/2
enddo


do i 0 $ncells
place cavity z=$i*$cell_length+$cell_length/2-($rf_length+2*$wallthick+$wallthick+$rf_length/2) rename=rf_b_$i
place cavity z=$i*$cell_length+$cell_length/2-($wallthick+$rf_length/2) rename=rf_c_$i
place cavity z=$i*$cell_length+$cell_length/2+($wallthick+$rf_length/2) rename=rf_d_$i
place cavity z=$i*$cell_length+$cell_length/2+($rf_length+2*$wallthick+$wallthick+$rf_length/2) rename=rf_e_$i
enddo

do i 0 $ncells
place all_wedge z=$i*$cell_length  rotation=X90,Y90 x=$x_off+$wedge_x_off rename=wedge_$i
enddo

do i 0 $ncells
place wedge_window z=$i*$cell_length-$half_wedge_length/2-$wedge_window_length/(cos($wedge_apex/2*$pi/180)) x=$x_off+$wedge_x_off rotation=Y-$wedge_apex/2 color=1,0,1
place wedge_window z=$i*$cell_length+$half_wedge_length/2+$wedge_window_length/(cos($wedge_apex/2*$pi/180)) x=$x_off+$wedge_x_off rotation=Y$wedge_apex/2 color=1,0,1
enddo


reference particle=mu+ referenceMomentum=$ref_momentum beamT=1657.83 beamX=$x_off beamY=$y_off
beam ascii file=beam_stage9.beam firstEvent=$first lastEvent=$last beamX=$x_off-$x_last beamY=$y_off-$y_last

trackcuts killSecondaries=1
#zntuple z=$cell_length filename=final
trace nTrace=1

#printfield type=grid file=field_cell.dat field=Bx,By,Bz X0=-180 Y0=-180 Z0=0 nX=61 dX=6 nY=61 dY=6 nZ=201 dZ=$cell_length/200
zntuple output zloop=0:$ncells*$cell_length:$cell_length format=for009 require=PDGid==-13 file=particles_info.txt referenceParticle=1