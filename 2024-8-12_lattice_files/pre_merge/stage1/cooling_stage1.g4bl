physics QGSP_BERT_EMX 

param -unset first=1
param -unset last=10

param ncells=70
param pi=3.1415926
param coilname=coil
param cell_length=1800
param solpos=0.2502498737213368
param coil_length=286.5827097351253
param coil_radius=400
param coil_width=150
param current=57.636236464210455
param angle=1.5
param bendpos=0.1
param dipole=0.0653*0.6
param dipole_length=100
param pipe_radius=300
param pipe_diameter=2*$pipe_radius
param fringe_length=4*$pipe_radius
param rf_length=190
param rf_fre=0.352
param rf_grad=27.4 #25.82890576920952
param rf_ph=18.528826484341735
param wedge_window_length=0.3
param rf_window_length=0.12
param wallthick=$rf_window_length
param half_wedge_length=145
param pipe_length=($cell_length-6*($rf_length+2*$wallthick))/4
param wedge_apex=45
param x_last=-6.871376142
param y_last=3.583457577
param x_off=-43.314727474874466
param y_off=-17.89829093590234
param ref_momentum=253

param pres=35

material H2 Z=1 A=1.01 density=0.000090*$pres pressure=$pres
param cavity_gas=Vacuum
#material cavity_gas H,0.99 O,0.01 density=0.0035 pressure=$pres
#param worldMaterial=H2

genericbend solbend fieldWidth=1000 fieldHeight=1000 fieldLength=$dipole_length \
               ironWidth=-1 ironHeight=-1 ironLength=-1 \
               openAperture=1 \
               By=$dipole kill=1 fringe=0 ironColor=1,1,1


fieldexpr fringe_left length=$fringe_length width=2*$pipe_radius height=2*$pipe_radius By=$dipole*((1+exp(5.5*-z/$pipe_diameter)*cos(5.5*y/$pipe_diameter))/(1+2*exp(5.5*-z/$pipe_diameter)*cos(5.5*y/$pipe_diameter)+exp(2*5.5*-z/$pipe_diameter))) Bz=-$dipole*exp(5.5*-z/$pipe_diameter)*sin(5.5*y/$pipe_diameter)/(1+2*exp(5.5*-z/$pipe_diameter)*cos(5.5*y/$pipe_diameter)+exp(2*5.5*-z/$pipe_diameter)) nX=2 nY=100 nZ=150 
fieldexpr fringe_right length=$fringe_length width=2*$pipe_radius height=2*$pipe_radius By=$dipole*((1+exp(5.5*z/$pipe_diameter)*cos(5.5*y/$pipe_diameter))/(1+2*exp(5.5*z/$pipe_diameter)*cos(5.5*y/$pipe_diameter)+exp(2*5.5*z/$pipe_diameter))) Bz=-$dipole*exp(5.5*z/$pipe_diameter)*sin(5.5*y/$pipe_diameter)/(1+2*exp(5.5*z/$pipe_diameter)*cos(5.5*y/$pipe_diameter)+exp(2*5.5*z/$pipe_diameter)) nX=2 nY=100 nZ=150 

coil $coilname innerRadius=$coil_radius outerRadius=$coil_radius+$coil_width length=$coil_length material=Cu maxZ=$cell_length maxR=$coil_radius+4*$coil_width #tolerance=0.02
solenoid sol1 coilName=$coilname current=$current kill=1 
solenoid sol2 coilName=$coilname current=-$current kill=1 

tubs pipe innerRadius=$pipe_radius-20 outerRadius=$pipe_radius-20+1 length=$pipe_length kill=1

pillbox cavity innerLength=$rf_length irisRadius=$pipe_radius-20 frequency=$rf_fre maxGradient=$rf_grad phaseAcc=$rf_ph winMat=Be cavityMaterial=$cavity_gas win1Thick=$rf_window_length win2Thick=$rf_window_length wallThick=$wallthick collarThick=0 kill=1

box wedge_window height=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) length=$wedge_window_length width=2*$half_wedge_length/2/sin($wedge_apex/2*$pi/180) material=Be
trap all_wedge  height=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) upperWidth=0.001 lowerWidth=2*$half_wedge_length length=2*$half_wedge_length/2/tan($wedge_apex/2*$pi/180) material=LH2


do i -2 $ncells
place sol1 z=$i*$cell_length+$solpos*$cell_length rename=sol_a_$i
place sol2 z=$i*$cell_length+$cell_length-$solpos*$cell_length rename=sol_b_$i
enddo

do i -2 $ncells
place solbend z=$i*$cell_length+$bendpos*$cell_length rename=solbend_a_$i
place solbend z=$i*$cell_length+$cell_length-$bendpos*$cell_length rename=solbend_b_$i
enddo

do i -2 $ncells
place fringe_left z=$i*$cell_length+$bendpos*$cell_length-$dipole_length/2-$fringe_length/2
place fringe_right z=$i*$cell_length+$bendpos*$cell_length+$dipole_length/2+$fringe_length/2+0.01
place fringe_left z=$i*$cell_length+$cell_length-$bendpos*$cell_length-$dipole_length/2-$fringe_length/2
place fringe_right z=$i*$cell_length+$cell_length-$bendpos*$cell_length+$dipole_length/2+$fringe_length/2+0.01
enddo

do i 0 $ncells
place pipe z=$i*$cell_length+$pipe_length/2
place pipe z=$i*$cell_length+$cell_length/2-$pipe_length/2
place pipe z=$i*$cell_length+$cell_length/2+$pipe_length/2
place pipe z=$i*$cell_length+$cell_length-$pipe_length/2
enddo


do i 0 $ncells
place cavity z=$i*$cell_length+$cell_length/4-(2*$wallthick+$rf_length) rename=rf_a_$i
place cavity z=$i*$cell_length+$cell_length/4 rename=rf_b_$i
place cavity z=$i*$cell_length+$cell_length/4+(2*$wallthick+$rf_length) rename=rf_c_$i

place cavity z=$i*$cell_length+3*$cell_length/4-(2*$wallthick+$rf_length) rename=rf_d_$i
place cavity z=$i*$cell_length+3*$cell_length/4 rename=rf_e_$i
place cavity z=$i*$cell_length+3*$cell_length/4+(2*$wallthick+$rf_length) rename=rf_f_$i
enddo

do i 0 $ncells
place all_wedge z=$i*$cell_length  rotation=X90,Y90 x=$x_off rename=wedge_$i
place all_wedge z=$i*$cell_length+$cell_length/2 rotation=X90,Y90 x=$x_off rename=wedge_b_$i
enddo

do i 0 $ncells
place wedge_window z=$i*$cell_length-$half_wedge_length/2-$wedge_window_length/(cos($wedge_apex/2*$pi/180)) x=$x_off rotation=Y-$wedge_apex/2 color=1,0,1
place wedge_window z=$i*$cell_length+$half_wedge_length/2+$wedge_window_length/(cos($wedge_apex/2*$pi/180)) x=$x_off rotation=Y$wedge_apex/2 color=1,0,1
place wedge_window z=$i*$cell_length+$cell_length/2-$half_wedge_length/2-$wedge_window_length/(cos($wedge_apex/2*$pi/180)) x=$x_off rotation=Y-$wedge_apex/2 color=1,0,1
place wedge_window z=$i*$cell_length+$cell_length/2+$half_wedge_length/2+$wedge_window_length/(cos($wedge_apex/2*$pi/180)) x=$x_off rotation=Y$wedge_apex/2 color=1,0,1
enddo


reference particle=mu+ referenceMomentum=$ref_momentum beamT=0 beamX=$x_off beamY=$y_off
beam ascii file=beam_stage1_352MHz.beam firstEvent=$first lastEvent=$last beamX=$x_off-$x_last beamY=$y_off-$y_last

trackcuts killSecondaries=1
#zntuple z=$cell_length filename=final
trace nTrace=1

#printfield type=grid file=field_cell.dat field=Bx,By,Bz X0=-180 Y0=-180 Z0=0 nX=61 dX=6 nY=61 dY=6 nZ=201 dZ=$cell_length/200
zntuple output zloop=0:$ncells*$cell_length:$cell_length format=for009 require=PDGid==-13 file=particles_info.txt referenceParticle=1